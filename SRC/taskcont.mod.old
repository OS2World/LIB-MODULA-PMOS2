IMPLEMENTATION MODULE TaskControl;

<* IF NOT multithread THEN *>
  "This module needs the multithread model"
  END TaskControl.
<* END *>

        (****************************************************************)
        (*                                                              *)
        (*   Data structures internal to the kernel of the operating    *)
        (*     system; the dispatcher of the operating system; and      *)
        (*                  related procedures.                         *)
        (*                                                              *)
        (*      Programmer:     P. Moylan                               *)
        (*      Last edited:    13 December 1999                        *)
        (*      Status:         Testing XDS 2.32                        *)
        (*                                                              *)
        (*    Module DumpFile is still in use to catch multiple         *)
        (*    tries needed to start a task.  In principle, the file     *)
        (*    DUMP.$$$ should never be created.  If I can confirm       *)
        (*    this, I can remove the "multiple tries" code.             *)
        (*                                                              *)
        (****************************************************************)

FROM DumpFile IMPORT DumpString, DumpCard, DumpHex, DumpEOL;

FROM SYSTEM IMPORT
    (* type *)  ADDRESS,
    (* proc *)  CAST;

IMPORT OS2,STextIO, Processes;

FROM IOChan IMPORT
     (* type *) ChanId;

FROM Storage IMPORT
    (* proc *)  ALLOCATE, DEALLOCATE;

FROM FinalExit IMPORT
    (* proc *)  Crash;

(************************************************************************)

CONST StackSize = 32768;

TYPE
    (********************************************************************)
    (*                                                                  *)
    (* Descriptor for a Lock.  The fields are:                          *)
    (*      next        the next lock held by the same task             *)
    (*      mutex       critical section protection                     *)
    (*                                                                  *)
    (********************************************************************)

    Lock = POINTER TO LockInfo;
    LockInfo = RECORD
                   next: Lock;
                   mutex: OS2.HMTX;
               END (*RECORD*);

    (********************************************************************)
    (*                                                                  *)
    (* Descriptor for a task.  The fields have the following meaning.   *)
    (*                                                                  *)
    (*   next        pointer to the next descriptor on the master task  *)
    (*                list                                              *)
    (*   name        identifier for testing purposes                    *)
    (*   WakeUp      event semaphore used in blocking a task            *)
    (*   id          thread identifier                                  *)
    (*   LockList    head of the list of locks held by this task        *)
    (*                                                                  *)
    (********************************************************************)

    Task = POINTER TO
               RECORD
                   next: Task;
                   name: NameString;
                   WakeUp: OS2.HEV;
                   id: TaskID;
                   LockList: Lock;
               END (*RECORD*);

(************************************************************************)

VAR
    (* The list of all tasks known to us. *)

    MasterTaskList: Task;

    (* Mutual exclusion semaphore.  We must lock this for any access to *)
    (* the task list.                                                   *)

    TaskListAccess: OS2.HMTX;

(************************************************************************)
(*                 KERNEL CRITICAL SECTION PROTECTION                   *)
(************************************************************************)

PROCEDURE LockTaskList;

    BEGIN
        OS2.DosRequestMutexSem (TaskListAccess, OS2.SEM_INDEFINITE_WAIT);
    END LockTaskList;

(************************************************************************)

PROCEDURE UnlockTaskList;

    BEGIN
        OS2.DosReleaseMutexSem (TaskListAccess);
    END UnlockTaskList;

(************************************************************************)
(*                            TASK CREATION                             *)
(************************************************************************)

PROCEDURE NewTaskDescriptor (taskname: NameString): Task;

    (* Creates a descriptor for a new task, and adds it to the master   *)
    (* task list.  Note that this does not fill in the id field of      *)
    (* the descriptor.                                                  *)

    VAR result: Task;

    BEGIN
        NEW (result);
        WITH result^ DO
            name := taskname;
            OS2.DosCreateEventSem (NIL, WakeUp, 0, FALSE);
            LockList := NIL;
        END (*WITH*);
        LockTaskList;
        result^.next := MasterTaskList;
        MasterTaskList := result;
        UnlockTaskList;
        RETURN result;
    END NewTaskDescriptor;

(************************************************************************)

TYPE TaskStartInfo = POINTER TO
                          RECORD
                              CASE HasParameter: BOOLEAN OF
                                 | FALSE:  TaskCode0: PROC;
                                 | TRUE:   TaskCode1: PROC1;
                              END (*CASE*);
                              parameter: ADDRESS;
                              StartConfirmation: OS2.HEV;
                          END;

(************************************************************************)

PROCEDURE TaskWrapper;

    (* This is the task that runs the user's task code. *)

    VAR StartInfo: TaskStartInfo;
        UseParameter: BOOLEAN;
        Proc0: PROC;
        Proc1: PROC1;  param: ADDRESS;

    BEGIN
        StartInfo := Processes.MyParam();
        WITH StartInfo^ DO
            OS2.DosPostEventSem (StartConfirmation);
            UseParameter := HasParameter;
            Proc0 := TaskCode0;
            Proc1 := TaskCode1;
            param := parameter;
        END (*WITH*);
        (*DumpString ("TaskWrapper1 starting task");  DumpEOL;*)

        (* Call the user's task code. *)

        IF UseParameter THEN
            Proc1 (param);
        ELSE
            Proc0;
        END (*IF*);

        TaskExit;

    END TaskWrapper;

(************************************************************************)

CONST patience = 800;     (* milliseconds *)

(************************************************************************)

PROCEDURE CreateTask (StartAddress: PROC;  taskpriority: PriorityLevel;
                                                taskname: NameString);

    (* Must be called to introduce a task to the system. The first      *)
    (* parameter, which should be the name of a procedure containing    *)
    (* the task code, gives the starting address.  The second parameter *)
    (* is the task's base priority.  If this task has a higher priority *)
    (* than its creator, it will run immediately.  Otherwise, it        *)
    (* becomes ready.                                                   *)

    VAR T: Task;  StartInfo: TaskStartInfo;
        tries: CARDINAL;

    BEGIN
        T := NewTaskDescriptor (taskname);
        NEW (StartInfo);
        WITH StartInfo^ DO
            HasParameter := FALSE;
            TaskCode0 := StartAddress;
            parameter := NIL;
            OS2.DosCreateEventSem (NIL, StartConfirmation, 0, FALSE);
        END (*WITH*);

        (* The following loop is to work around a bug: for some unknown *)
        (* reason, Processes.Activate does not always succeed in        *)
        (* activating the new thread.  To get around this, we require   *)
        (* the newly started thread to confirm that it has started.     *)
        (* In principle I can remove this complexity, because the bug   *)
        (* appears to have been fixed, but I'm leaving the test in for  *)
        (* a trial period.                                              *)

        tries := 0;
        Processes.Create (TaskWrapper, StackSize, taskpriority, StartInfo, T^.id);
        REPEAT
            Processes.Activate (T^.id);
            INC (tries);
        UNTIL OS2.DosWaitEventSem (StartInfo^.StartConfirmation,
                                   patience) <> OS2.ERROR_TIMEOUT;

        IF tries > 1 THEN
            DumpString ("CreateTask created ");  DumpString (taskname);
            DumpString (" after ");  DumpCard (tries);
            DumpString (" tries.");  DumpEOL;
        END (*IF*);

        OS2.DosCloseEventSem (StartInfo^.StartConfirmation);
        DISPOSE (StartInfo);

    END CreateTask;

(************************************************************************)

PROCEDURE CreateTask1 (StartAddress: PROC1;  taskpriority: PriorityLevel;
                                   taskname: NameString;  param: ADDRESS);

    (* Like CreateTask, but allows the passing of a single parameter    *)
    (* "param" to the task.                                             *)

    VAR StartInfo: TaskStartInfo;  T: Task;
        tries: CARDINAL;

    BEGIN
        T := NewTaskDescriptor (taskname);
        NEW (StartInfo);
        WITH StartInfo^ DO
            HasParameter := TRUE;
            TaskCode1 := StartAddress;  parameter := param;
            OS2.DosCreateEventSem (NIL, StartConfirmation, 0, FALSE);
        END (*WITH*);

        (* The following loop is to work around a bug: for some unknown *)
        (* reason, Processes.Activate does not always succeed in        *)
        (* activating the new thread.  To get around this, we require   *)
        (* the newly started thread to confirm that it has started.     *)
        (* In principle I can remove this complexity, because the bug   *)
        (* appears to have been fixed, but I'm leaving the test in for  *)
        (* a trial period.                                              *)

        tries := 0;
        Processes.Create (TaskWrapper, StackSize, taskpriority, StartInfo, T^.id);
        REPEAT
            Processes.Activate (T^.id);
            INC (tries);
        UNTIL OS2.DosWaitEventSem (StartInfo^.StartConfirmation,
                                         patience) <> OS2.ERROR_TIMEOUT;

        IF tries > 1 THEN
            DumpString ("CreateTask created ");  DumpString (taskname);
            DumpString (" after ");  DumpCard (tries);
            DumpString (" tries.");  DumpEOL;
        END (*IF*);

        OS2.DosCloseEventSem (StartInfo^.StartConfirmation);
        DISPOSE (StartInfo);

    END CreateTask1;

(************************************************************************)

PROCEDURE TaskExit;

    (* Removes the currently running task from the system, and performs *)
    (* a task switch to the next ready task.  In the present version    *)
    (* we don't bother discarding the task descriptor, because there's  *)
    (* not much overhead in letting it stay in the master list.         *)

    BEGIN
        Processes.StopMe;
    END TaskExit;

(************************************************************************)
(*                        IDENTIFYING A TASK                            *)
(************************************************************************)

PROCEDURE CurrentTaskID(): TaskID;

    (* Returns the TaskID of the calling task. *)

    BEGIN
        RETURN Processes.Me();
    END CurrentTaskID;

(************************************************************************)

PROCEDURE DescriptorOf (id: TaskID): Task;

    (* Returns the task descriptor corresponding to the given TaskID. *)
    (* Assumption: the caller has already locked the task list.       *)

    VAR result: Task;

    BEGIN
        result := MasterTaskList;
        WHILE (result <> NIL) AND (result^.id <> id) DO
            result := result^.next;
        END (*WHILE*);
        RETURN result;
    END DescriptorOf;

(************************************************************************)
(*                LOCKS FOR CRITICAL SECTION PROTECTION                 *)
(************************************************************************)

PROCEDURE CreateLock (VAR (*OUT*) L: Lock);

    (* Creates a new lock. *)

    BEGIN
        NEW (L);
        L^.next := NIL;
        OS2.DosCreateMutexSem (NIL, L^.mutex, 0, FALSE);
    END CreateLock;

(************************************************************************)

PROCEDURE DestroyLock (VAR (*INOUT*) L: Lock);

    (* Disposes of a lock. *)

    BEGIN
        IF OS2.DosCloseMutexSem (L^.mutex) <> 0 THEN
        END (*IF*);
        DISPOSE (L);
    END DestroyLock;

(************************************************************************)

PROCEDURE Obtain (L: Lock);

    (* Obtains lock L, waiting if necessary. *)

    VAR T: Task;

    BEGIN
        OS2.DosRequestMutexSem (L^.mutex, OS2.SEM_INDEFINITE_WAIT);
        LockTaskList;
        T := DescriptorOf (Processes.Me());
        L^.next := T^.LockList;
        T^.LockList := L;
        UnlockTaskList;
    END Obtain;

(************************************************************************)

PROCEDURE Release (L: Lock);

    (* Releases lock L - which might unblock some other task. *)

    VAR previous, current: Lock;  T: Task;

    BEGIN
        LockTaskList;
        T := DescriptorOf (Processes.Me());
        previous := NIL;  current := T^.LockList;
        LOOP
            IF current = NIL THEN
                Crash ("Releasing a lock we don't hold");
            END (*IF*);
            IF current = L THEN
                IF previous = NIL THEN
                    T^.LockList := current^.next;
                ELSE
                    previous^.next := current^.next;
                END (*IF*);
                EXIT (*LOOP*);
            END (*IF*);
            previous := current;  current := current^.next;
        END (*LOOP*);
        UnlockTaskList;
        OS2.DosReleaseMutexSem (L^.mutex);
    END Release;

(************************************************************************)

PROCEDURE ReleaseAllLocks;

    (* Releases all locks held by the current task.  Application-level  *)
    (* tasks normally won't need to call this procedure; it is          *)
    (* provided to support the system shutdown function and for things  *)
    (* like "emergency abort" operations.                               *)

    VAR T: Task;

    BEGIN
        LockTaskList;
        T := DescriptorOf (Processes.Me());
        WHILE T^.LockList <> NIL DO
            OS2.DosReleaseMutexSem (T^.LockList^.mutex);
            T^.LockList := T^.LockList^.next;
        END (*WHILE*);
        UnlockTaskList;
    END ReleaseAllLocks;

(************************************************************************)
(*                 SUSPENDING AND RESUMING A TASK                       *)
(************************************************************************)

PROCEDURE SuspendMe (id: TaskID;  TimeLimit: CARDINAL): BOOLEAN;

    (* Suspends the caller.  A TRUE result indicates that the time      *)
    (* limit expired without the task being woken up.                   *)

    VAR T: Task;  status: CARDINAL;  PostCount: CARDINAL;
        TimedOut: BOOLEAN;

    BEGIN
        T := DescriptorOf (id);
        status := OS2.DosWaitEventSem (T^.WakeUp, TimeLimit);
        TimedOut := status = OS2.ERROR_TIMEOUT;
        IF NOT TimedOut THEN
            status := OS2.DosResetEventSem (T^.WakeUp, PostCount);
        END (*IF*);
        RETURN TimedOut;
    END SuspendMe;

(************************************************************************)

PROCEDURE ResumeTask (id: TaskID): BOOLEAN;

    (* Resumes a task specified by its thread ID.                       *)
    (* The function result is normally TRUE, but is FALSE if the task   *)
    (* couldn't be resumed (usually because that task no longer exists).*)

    VAR T: Task;  status: CARDINAL;

    BEGIN
        T := DescriptorOf (id);
        IF T = NIL THEN
            RETURN FALSE;
        END (*IF*);
        status := OS2.DosPostEventSem (T^.WakeUp);
        RETURN TRUE;
    END ResumeTask;

(************************************************************************)
(*                     MODULE INITIALISATION                            *)
(************************************************************************)

PROCEDURE CreateMainTaskDescriptor;

    VAR T: Task;

    BEGIN
        T := NewTaskDescriptor ("*MAIN*");
        T^.id := Processes.Me();
    END CreateMainTaskDescriptor;

(************************************************************************)

BEGIN
    OS2.DosCreateMutexSem (NIL, TaskListAccess, 0, FALSE);
    MasterTaskList := NIL;
    CreateMainTaskDescriptor;
END TaskControl.

